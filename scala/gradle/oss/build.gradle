plugins {
    id "scala"
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

group 'com.datastax.spark.example'
version '0.1'

repositories {
    mavenCentral()
}

def sparkVersion = "1.6.2"
def connectorVersion = "1.6.3"
def scalaVersion = "2.10"
def scalaTestVersion = "3.0.0"
def cassandraVersion = "3.0.2"
def jUnitVersion = "4.12"

// The assembly configuration will cause jar to be included in assembled fat-jar
configurations {
    assembly
    compile.extendsFrom assembly
}

// The provided configuration behaves the same as the sbt "provided" keyword which will cause jars to be
// excluded from assembled fat-jar
configurations {
    provided
    compile.extendsFrom provided
}

// Please make sure that following dependencies have versions corresponding to the ones in your cluster.
// Note that spark-cassandra-connector should be provided with '--packages' flag to spark-submit command.
dependencies {
    provided "org.apache.spark:spark-core_$scalaVersion:$sparkVersion"
    provided "org.apache.spark:spark-sql_$scalaVersion:$sparkVersion"
    provided "org.apache.spark:spark-hive_$scalaVersion:$sparkVersion"
    provided "com.datastax.spark:spark-cassandra-connector_$scalaVersion:$connectorVersion"
//  assembly "org.apache.commons:commons-math3:3.6.1"
//  assembly "org.apache.commons:commons-csv:1.0"

    //Test Dependencies
    testCompile ("com.datastax.spark:spark-cassandra-connector-embedded_$scalaVersion:$connectorVersion") {
        exclude group: 'com.datastax.cassandra', module: 'cassandra-driver-core'
    }
    testCompile "org.scalatest:scalatest_$scalaVersion:$scalaTestVersion"
    testCompile ("org.apache.cassandra:cassandra-all:$cassandraVersion") {
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'  // Excluded to allow for Cassandra to run embedded
    }
    testCompile "junit:junit:$jUnitVersion"
}

shadowJar {
    configurations = [project.configurations.assembly]
}

//shadowJar {
//  relocate 'org.apache.commons.csv', 'shaded.org.apache.commons.csv'
//}
