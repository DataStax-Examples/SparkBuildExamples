plugins {
    id "scala"
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

group 'com.datastax.spark.example'
version '0.1'

repositories {
    mavenCentral()
    maven {
        url "https://datastax.artifactoryonline.com/datastax/public-repos/"
    }
}


// The assembly configuration will cause jar to be included in assembled fat-jar
configurations {
    assembly
    compile.extendsFrom assembly
}

// The provided configuration behaves the same as the sbt "provided" keyword which will cause jars to be
// excluded from assembled fat-jar
configurations {
    provided
    compile.extendsFrom provided
    testCompile.exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

def dseVersion = "5.1.0"

def scalaVersion = "2.11"
def scalaTestVersion = "3.0.0"
def connectorVersion = "2.0.1"
def cassandraVersion = "3.2"
def jUnitVersion = "4.12"

// Please make sure that following DSE version matches your DSE cluster version.
dependencies {
    provided("com.datastax.dse:dse-spark-dependencies:$dseVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.apache.cassandra'
        exclude group: 'com.datastax.dse', module: 'dse-java-driver-core'
        exclude group: 'org.apache.solr', module: 'solr-solrj'
    }
//  assembly "org.apache.commons:commons-math3:3.6.1"
//  assembly "org.apache.commons:commons-csv:1.0"
    provided "com.datastax.dse:dse-java-driver-core:1.2.3"
    provided "org.apache.solr:solr-solrj:6.0.1"

    // Test Dependencies
    testCompile "com.datastax.spark:spark-cassandra-connector-embedded_$scalaVersion:$connectorVersion"
    testCompile "org.scalatest:scalatest_$scalaVersion:$scalaTestVersion"
    testCompile "org.apache.cassandra:cassandra-all:$cassandraVersion"
    testCompile "junit:junit:$jUnitVersion"
}

shadowJar {
    configurations = [project.configurations.assembly]
}

//shadowJar {
//  relocate 'org.apache.commons.csv', 'shaded.org.apache.commons.csv'
//}