group 'com.datastax.spark.example'
version '0.1'
apply plugin: 'scala'

repositories {
    mavenCentral()
    maven {
        url "https://datastax.artifactoryonline.com/datastax/public-repos/"
    }
}

// The provided configuration behaves the same as the sbt "provided" keyword which will cause jars to be
// excluded from assembled fat-jar
configurations {
    provided
    compile.extendsFrom provided
    testCompile.exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

def scalaVersion = "2.10"
def connectorVersion = "1.6.0"
def scalaTestVersion = "3.0.0"

// Please make sure that following DSE version matches your DSE cluster version.
dependencies {
    provided "com.datastax.dse:dse-spark-dependencies:5.0.1" exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    // compile "your:dependecy:1.0.0"

    //Test Dependencies
    testCompile "com.datastax.spark:spark-cassandra-connector-embedded_$scalaVersion:$connectorVersion"
    testCompile "org.scalatest:scalatest_$scalaVersion:$scalaTestVersion"
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}
